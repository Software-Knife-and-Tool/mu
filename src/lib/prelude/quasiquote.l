;;;  SPDX-FileCopyrightText: Copyright 2023 James M. Putnam (putnamjm.design@gmail.com)
;;;  SPDX-License-Identifier: MIT

;;;
;;; quasiquote
;;;
(mu:intern (mu:find-namespace "") "%quasi%" ())

(mu:intern prelude "%list"
   (:lambda (form)
     `(,form)))

(mu:intern prelude "%quasi-func-map"
   (:lambda (key map)
     (mu:cdr (prelude:assq key map))))

(mu:intern prelude "%quasi-read"
   (:lambda (stream)
     ((:lambda (ch syntax-map)
        (:if (prelude:null ch)
             (prelude:error stream "backquote: early end of file~%" ())
             ((:lambda (type-map)
                (:if (prelude:null type-map)
                     `(:form ,(prelude:%read-dispatch ch stream))
                     (mu:apply (mu:cdr type-map) ())))
                (prelude:assq ch syntax-map))))
        (prelude:%read-consume-ws stream)
        `(,(mu:cons #\( (:lambda () `(:list ,(prelude:%quasi-read-list stream))))
           ,(mu:cons #\) (:lambda () `(:list-)))
           ,(mu:cons #\, (:lambda ()
                           ((:lambda (ch)
                             (:if (prelude:null ch)
                                  (prelude:error stream "backquote: early end of file~%" ())
                                  (:if (mu:eq #\@ ch)
                                       `(:comma@ ,(prelude:read stream () ()))
                                       ((:lambda ()
                                          (prelude:unread-char ch stream)
                                          `(:form ,(prelude:read stream () ())))))))
                            (prelude:read-char stream () ()))))
           ,(mu:cons #\' (:lambda () `(:qquote ,(prelude:%quasi-read stream))))
           ,(mu:cons #\` (:lambda () `(:quasi ,(prelude:read stream () ()))))))))

(mu:intern prelude "%quasi-read-list"
   (:lambda (stream)
     ((:lambda (syntax-map)
         (mu:cdr
          (mu:fix
           (:lambda (loop)
             ((:lambda (eol list)
                (:if eol
                     loop
                     ((:lambda (syntax)
                        ((:lambda (type expr)
                           (:if (prelude:null syntax)
                                (prelude:error stream "qquote: early end of file~%" ())
                                ((:lambda (type-fn)
                                   (mu:apply type-fn `(,expr ,list)))
                                 (prelude:%quasi-func-map type syntax-map))))
                         (mu:car syntax)
                         (mu:cdr syntax)))
                      (prelude:%quasi-read stream))))
              (mu:car loop)
              (mu:cdr loop)))
           ())))
      `(,(mu:cons :comma   (:lambda (expr list)
                             (mu:cons () (mu:append list (prelude:%list `(:l-comma ,(mu:car expr)))))))
         ,(mu:cons :comma@ (:lambda (expr list)
                             (mu:cons () (mu:append list (prelude:%list `(:comma@ ,(mu:car expr)))))))
         ,(mu:cons :form   (:lambda (expr list)
                             (mu:cons () (mu:append list (prelude:%list `(:l-form ,(mu:car expr)))))))
         ,(mu:cons :list   (:lambda (expr list)
                             (:if (prelude:null (mu:car expr))
                                  (mu:cons () (mu:append list (prelude:%list `(:l-form ()))))
                                  (:if (prelude:consp (mu:car expr))
                                       ((:lambda (form)
                                          (mu:cons () (mu:append list (prelude:%list `(:l-form ,form)))))
                                        (prelude:mapcar (:lambda (expr) (mu:nth 1 expr)) (mu:car expr)))
                                       (mu:cons () (mu:append list (prelude:%list `(:l-list ,(mu:car expr)))))))))
         ,(mu:cons :list-  (:lambda (expr list)
                             (mu:cons :t list)))
         ,(mu:cons :quasi  (:lambda (expr list)
                             (mu:cons () (mu:append list `(,(prelude:%quasi-read stream))))))))))

(mu:intern prelude "%quasi-compile"
   (:lambda (quasi-expr)
     ((:lambda (type expr compiler-map)
        ((:lambda (type-fn)
           (:if (prelude:null type-fn)
                (prelude:error type "backquote: unmapped type ~A~%" `(,type))
                (mu:apply type-fn `(,expr))))
         (prelude:%quasi-func-map type compiler-map)))
      (mu:nth 0 quasi-expr)
      (mu:nth 1 quasi-expr)
      `(,(mu:cons :l-comma  (:lambda (expr) `(mu:cons ,expr ())))
         ,(mu:cons :l-list  (:lambda (expr)
                              ((:lambda (loop)
                                 (mu:apply loop `(,loop ,expr)))
                               (:lambda (loop list)
                                 (:if (prelude:null list)
                                      list
                                      `(mu:append
                                        ,(prelude:%quasi-compile (mu:car list))
                                        ,(mu:apply loop `(,loop ,(mu:cdr list)))))))))
         ,(mu:cons :l-form  (:lambda (expr)
                              ((:lambda (quote)
                               `(mu:cons ,quote ()))
                               `(:quote ,expr))))
         ,(mu:cons :comma@ (:lambda (expr) expr))
         ,(mu:cons :list    (:lambda (list)
                              ((:lambda (loop)
                                 (mu:apply loop `(,loop ,list)))
                               (:lambda (loop list)
                                 (:if (prelude:null list)
                                      list
                                      `(mu:append
                                        ,(prelude:%quasi-compile (mu:car list))
                                        ,(mu:apply loop `(,loop ,(mu:cdr list)))))))))
         ,(mu:cons :form    (:lambda (expr)
                              `(:quote ,expr)))
         ,(mu:cons :quasi   (:lambda (expr)
                              ((:lambda (type)
                                 (:if (mu:eq :form type)
                                      (mu:cdr expr)
                                      (:if (mu:eq :quote type)
                                           (prelude:%quasi-compile expr)
                                           ())))
                               (mu:car expr))))
         ,(mu:cons :qquote   (:lambda (expr)
                               `(:quote ,(prelude:%quasi-compile expr))))))))

(mu:intern prelude "%read-quasi"
   (:lambda (char stream)
     `(%quasi% ,(prelude:%quasi-read stream))))
