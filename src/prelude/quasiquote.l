;;;  SPDX-FileCopyrightText: Copyright 2023 James M. Putnam (putnamjm.design@gmail.com)
;;;  SPDX-License-Identifier: MIT

;;;
;;; quasiquote
;;;
(crux:intern (crux:find-ns "") "%quasi%" ())

(crux:intern prelude "%list"
   (:lambda (form)
     `(,form)))

(crux:intern prelude "%quasi-func-map"
   (:lambda (key map)
     (crux:cdr (prelude:assq key map))))

(crux:intern prelude "%quasi-read"
   (:lambda (stream)
     ((:lambda (ch syntax-map)
        (:if (prelude:null ch)
             (prelude:error stream "backquote: early end of file~%" ())
             ((:lambda (type-map)
                (:if (prelude:null type-map)
                     `(:form ,(prelude:%read-dispatch ch stream))
                     (crux:apply (crux:cdr type-map) ())))
                (prelude:assq ch syntax-map))))
        (prelude:%read-consume-ws stream)
        `(,(crux:cons #\( (:lambda () `(:list ,(prelude:%quasi-read-list stream))))
           ,(crux:cons #\) (:lambda () `(:list-)))
           ,(crux:cons #\, (:lambda ()
                           ((:lambda (ch)
                             (:if (prelude:null ch)
                                  (prelude:error stream "backquote: early end of file~%" ())
                                  (:if (crux:eq #\@ ch)
                                       `(:comma@ ,(prelude:read stream () ()))
                                       ((:lambda ()
                                          (prelude:unread-char ch stream)
                                          `(:form ,(prelude:read stream () ())))))))
                            (prelude:read-char stream () ()))))
           ,(crux:cons #\' (:lambda () `(:qquote ,(prelude:%quasi-read stream))))
           ,(crux:cons #\` (:lambda () `(:quasi ,(prelude:read stream () ()))))))))

(crux:intern prelude "%quasi-read-list"
   (:lambda (stream)
     ((:lambda (syntax-map)
         (crux:cdr
          (crux:fix
           (:lambda (loop)
             ((:lambda (eol list)
                (:if eol
                     loop
                     ((:lambda (syntax)
                        ((:lambda (type expr)
                           (:if (prelude:null syntax)
                                (prelude:error stream "qquote: early end of file~%" ())
                                ((:lambda (type-fn)
                                   (crux:apply type-fn `(,expr ,list)))
                                 (prelude:%quasi-func-map type syntax-map))))
                         (crux:car syntax)
                         (crux:cdr syntax)))
                      (prelude:%quasi-read stream))))
              (crux:car loop)
              (crux:cdr loop)))
           ())))
      `(,(crux:cons :comma   (:lambda (expr list)
                             (crux:cons () (crux:append list (prelude:%list `(:l-comma ,(crux:car expr)))))))
         ,(crux:cons :comma@ (:lambda (expr list)
                             (crux:cons () (crux:append list (prelude:%list `(:comma@ ,(crux:car expr)))))))
         ,(crux:cons :form   (:lambda (expr list)
                             (crux:cons () (crux:append list (prelude:%list `(:l-form ,(crux:car expr)))))))
         ,(crux:cons :list   (:lambda (expr list)
                             (:if (prelude:null (crux:car expr))
                                  (crux:cons () (crux:append list (prelude:%list `(:l-form ()))))
                                  (:if (prelude:consp (crux:car expr))
                                       ((:lambda (form)
                                          (crux:cons () (crux:append list (prelude:%list `(:l-form ,form)))))
                                        (prelude:mapcar (:lambda (expr) (crux:nth 1 expr)) (crux:car expr)))
                                       (crux:cons () (crux:append list (prelude:%list `(:l-list ,(crux:car expr)))))))))
         ,(crux:cons :list-  (:lambda (expr list)
                             (crux:cons :t list)))
         ,(crux:cons :quasi  (:lambda (expr list)
                             (crux:cons () (crux:append list `(,(prelude:%quasi-read stream))))))))))

(crux:intern prelude "%quasi-compile"
   (:lambda (quasi-expr)
     ((:lambda (type expr compiler-map)
        ((:lambda (type-fn)
           (:if (prelude:null type-fn)
                (prelude:error type "backquote: unmapped type ~A~%" `(,type))
                (crux:apply type-fn `(,expr))))
         (prelude:%quasi-func-map type compiler-map)))
      (crux:nth 0 quasi-expr)
      (crux:nth 1 quasi-expr)
      `(,(crux:cons :l-comma  (:lambda (expr) `(crux:cons ,expr ())))
         ,(crux:cons :l-list  (:lambda (expr)
                              ((:lambda (loop)
                                 (crux:apply loop `(,loop ,expr)))
                               (:lambda (loop list)
                                 (:if (prelude:null list)
                                      list
                                      `(crux:append
                                        ,(prelude:%quasi-compile (crux:car list))
                                        ,(crux:apply loop `(,loop ,(crux:cdr list)))))))))
         ,(crux:cons :l-form  (:lambda (expr)
                              ((:lambda (quote)
                               `(crux:cons ,quote ()))
                               `(:quote ,expr))))
         ,(crux:cons :comma@ (:lambda (expr) expr))
         ,(crux:cons :list    (:lambda (list)
                              ((:lambda (loop)
                                 (crux:apply loop `(,loop ,list)))
                               (:lambda (loop list)
                                 (:if (prelude:null list)
                                      list
                                      `(crux:append
                                        ,(prelude:%quasi-compile (crux:car list))
                                        ,(crux:apply loop `(,loop ,(crux:cdr list)))))))))
         ,(crux:cons :form    (:lambda (expr)
                              `(:quote ,expr)))
         ,(crux:cons :quasi   (:lambda (expr)
                              ((:lambda (type)
                                 (:if (crux:eq :form type)
                                      (crux:cdr expr)
                                      (:if (crux:eq :quote type)
                                           (prelude:%quasi-compile expr)
                                           ())))
                               (crux:car expr))))
         ,(crux:cons :qquote   (:lambda (expr)
                               `(:quote ,(prelude:%quasi-compile expr))))))))

(crux:intern prelude "%read-quasi"
   (:lambda (char stream)
     `(%quasi% ,(prelude:%quasi-read stream))))
